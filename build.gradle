plugins {
  id "java"
  id "application"
  id "org.flywaydb.flyway" version "4.0.1"
  id 'com.github.johnrengelman.shadow' version '1.2.3'
  id "au.com.dius.pact" version "3.3.7"
}

apply plugin: 'idea'
apply plugin: 'maven-publish'

idea {
  module {
    inheritOutputDirs = false
    outputDir = file('build/classes/main')
    testOutputDir = file('build/classes/test')
  }
}

group = 'uk.gov.bis.lite'
version '1.0'

sourceCompatibility = 1.8

mainClassName = "uk.gov.bis.lite.ogel.OgelApplication"

ext.dropwizardVersion = '1.0.5'

repositories {
  mavenCentral()
  maven {
    name "snapshots"
    url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-snapshots"
  }
  maven {
    name "releases"
    url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-releases"
  }
}

dependencies {

  compile "io.dropwizard:dropwizard-core:$dropwizardVersion",
      "io.dropwizard:dropwizard-db:$dropwizardVersion",
      "io.dropwizard:dropwizard-jdbi:$dropwizardVersion",
      "io.dropwizard:dropwizard-auth:$dropwizardVersion",
      "io.dropwizard:dropwizard-client:$dropwizardVersion",
      'ru.vyarus:dropwizard-guicey:3.2.0',
      'com.fiestacabin.dropwizard.quartz:dropwizard-quartz:0.7.2',
      'org.xerial:sqlite-jdbc:3.8.11.2',
      'org.flywaydb:flyway-core:4.0.1',
      'uk.gov.bis.lite:jersey-correlation-id:1.0',
      'uk.gov.bis.lite:spire-client:1.2',
      'uk.gov.bis.lite:lite-control-code-service-api:1.0'

  testCompile 'junit:junit:4.12',
      'org.mockito:mockito-all:1.9.5',
      "io.dropwizard:dropwizard-testing:$dropwizardVersion",
      'org.skyscreamer:jsonassert:1.3.0'

  testCompile group: 'au.com.dius', name: 'pact-jvm-provider-junit_2.11', version: '3.3.7'
}

task apiJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.output.classesDir
  include 'uk/gov/bis/lite/ogel/api/**/*'
}

task jarWithSources(type: Jar) {
  from sourceSets.main.allJava
  include 'uk/gov/bis/lite/ogel/api/**/*'
}

publishing {
  publications {

    mavenJava(MavenPublication) {
      artifactId 'lite-ogel-service-api'
      artifact apiJar
      artifact jarWithSources {
        classifier "sources"
      }
    }
  }
}

publishing {
  repositories {
    maven {
      name "snapshots"
      url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-snapshots/"
    }
    maven {
      name "releases"
      url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-releases/"
    }
  }
}

flyway {
  url = 'jdbc:sqlite:localOgel.db'
}

shadowJar {
  classifier = ''
  mergeServiceFiles()
}

jar {
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

build {
  dependsOn shadowJar
}

pact {

  serviceProviders {

    // You can define as many as you need, but each must have a unique name
    "lite-ogel-service" {
      // All the provider properties are optional, and have sensible defaults (shown below)
      protocol = 'http'
      host = 'ogel-service.svc.dev.licensing.service.trade.gov.uk.test'
      port = 80
      path = '/'

      // Again, you can define as many consumers for each provider as you need, but each must have a unique name
      hasPactWith('lite-ogel-registration') {

        // currently supports a file path using file() or a URL using url()
        pactFile = file('/Users/Tomacpro/Projects/GitHub/lite-ogel-registration/target/pacts/lite-ogel-registration-lite-ogel-service.json')

      }
    }

  }

}

run { args 'server', 'src/main/resources/config.yaml' }
