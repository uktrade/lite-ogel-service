import java.text.SimpleDateFormat

plugins {
  id "java"
  id "application"
  id "org.flywaydb.flyway" version "4.0.1"
  id 'com.github.johnrengelman.shadow' version '1.2.3'
  id "au.com.dius.pact" version "3.3.10"
}

apply plugin: 'idea'

idea {
  module {
    inheritOutputDirs = false
    outputDir = file('build/classes/main')
    testOutputDir = file('build/classes/test')
  }
}

group 'uk.gov.bis.lite'

if (project.hasProperty('projVersion')) {
  project.version = project.projVersion
} else {
  SimpleDateFormat sdf = new SimpleDateFormat("YYYYMMdd.HHmmss")
  sdf.setTimeZone(new SimpleTimeZone(SimpleTimeZone.UTC_TIME, "UTC"))
  project.version = sdf.format(new Date()) + "-SNAPSHOT"
}

sourceCompatibility = 1.8

mainClassName = "uk.gov.bis.lite.ogel.OgelApplication"

ext.dropwizardVersion = '1.0.5'

repositories {
  mavenCentral()
  maven {
    name "snapshots"
    url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-snapshots"
  }
  maven {
    name "releases"
    url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-releases"
  }
}

dependencies {
  compile "io.dropwizard:dropwizard-core:$dropwizardVersion",
      "io.dropwizard:dropwizard-db:$dropwizardVersion",
      "io.dropwizard:dropwizard-jdbi:$dropwizardVersion",
      "io.dropwizard:dropwizard-auth:$dropwizardVersion",
      "io.dropwizard:dropwizard-client:$dropwizardVersion",
      'ru.vyarus:dropwizard-guicey:3.2.0',
      'com.fiestacabin.dropwizard.quartz:dropwizard-quartz:0.7.2',
      'org.xerial:sqlite-jdbc:3.8.11.2',
      'org.flywaydb:flyway-core:4.0.1',
      'net.logstash.logback:logstash-logback-encoder:4.10',
      'ch.qos.logback.contrib:logback-jackson:0.1.5',
      'uk.gov.bis.lite:json-console-appender:1.1',
      'uk.gov.bis.lite:jersey-correlation-id:1.0',
      'uk.gov.bis.lite:spire-client:1.3',
      'uk.gov.bis.lite:lite-control-code-service-api:1.0'

  testCompile 'junit:junit:4.12',
      'org.mockito:mockito-all:1.9.5',
      "io.dropwizard:dropwizard-testing:$dropwizardVersion",
      'org.skyscreamer:jsonassert:1.3.0'

  //Pact
  testCompile 'au.com.dius:pact-jvm-provider-junit_2.11:3.3.10'
  testCompile 'au.com.dius:pact-jvm-consumer-junit_2.11:3.3.10'
}

pact {
  publish {
    pactDirectory = 'target/pacts'
    pactBrokerUrl = 'http://pact-broker.mgmt.licensing.service.trade.gov.uk.test/'
    version = '1.0.0'
  }
}

flyway {
  url = 'jdbc:sqlite:localOgel.db'
}

shadowJar {
  classifier = ''
  mergeServiceFiles()
}

jar {
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

build {
  dependsOn shadowJar
}

run { args 'server', 'src/main/resources/config.yaml' }


apply plugin: 'maven-publish'

publishing {
  repositories {
    maven {
      name "snapshots"
      url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-snapshots/"
    }
    maven {
      name "releases"
      url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/maven-releases/"
    }
    maven {
      name "lite-builds"
      url "http://nexus.mgmt.licensing.service.trade.gov.uk.test/repository/lite-builds/"
    }
  }
}

task apiJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.output.classesDir
  include 'uk/gov/bis/lite/ogel/api/**/*'
}

task apiJarWithSources(type: Jar) {
  from sourceSets.main.allJava
  include 'uk/gov/bis/lite/ogel/api/**/*'
}

publishing {
  publications {
    api(MavenPublication) {
      artifactId 'lite-ogel-service-api'
      version '1.1'
      artifact apiJar
      artifact apiJarWithSources {
        classifier "sources"
      }
    }
    service(MavenPublication) {
      artifactId 'ogel-service'
      from components.shadow
    }
  }
}