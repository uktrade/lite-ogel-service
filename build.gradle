plugins {
  id "java"
  id "application"
  id "org.flywaydb.flyway" version "4.0.1"
  id 'com.github.johnrengelman.shadow' version '1.2.3'
  id "au.com.dius.pact" version "3.3.10"
}

apply plugin: 'idea'

idea {
  module {
    inheritOutputDirs = false
    outputDir = file('build/classes/main')
    testOutputDir = file('build/classes/test')
  }
}

group 'uk.gov.bis.lite'

project.version = project.hasProperty('projVersion') ? project.projVersion : '1.0-SNAPSHOT'

sourceCompatibility = 1.8

mainClassName = "uk.gov.bis.lite.ogel.OgelApplication"

ext.dropwizardVersion = '1.2.2'

repositories {
  mavenCentral()
  jcenter()
  maven {
    name "snapshots"
    url "https://nexus.ci.uktrade.io/repository/maven-snapshots"
  }
  maven {
    name "releases"
    url "https://nexus.ci.uktrade.io/repository/maven-releases"
  }
}

dependencies {
  compile "io.dropwizard:dropwizard-core:$dropwizardVersion",
      "io.dropwizard:dropwizard-db:$dropwizardVersion",
      "io.dropwizard:dropwizard-jdbi:$dropwizardVersion",
      "io.dropwizard:dropwizard-auth:$dropwizardVersion",
      "io.dropwizard:dropwizard-client:$dropwizardVersion",
      'ru.vyarus:dropwizard-guicey:3.2.0',
      'com.fiestacabin.dropwizard.quartz:dropwizard-quartz:0.7.2',
      'org.flywaydb:flyway-core:4.0.1',
      'net.logstash.logback:logstash-logback-encoder:4.10',
      'ch.qos.logback.contrib:logback-jackson:0.1.5',
      'uk.gov.bis.lite:json-console-appender:1.1',
      'uk.gov.bis.lite:jersey-correlation-id:1.0',
      'uk.gov.bis.lite:spire-client:1.6',
      'uk.gov.bis.lite:lite-control-code-service-api:1.1',
      'uk.gov.bis.lite:readiness-metric:1.0',
      'uk.gov.bis.lite:basic-auth:1.0',
      'org.postgresql:postgresql:42.1.4',
      'uk.gov.bis.lite:paas-utils:1.0',
      'org.apache.commons:commons-collections4:4.1'

  testCompile 'junit:junit:4.12',
      'org.mockito:mockito-all:1.9.5',
      "io.dropwizard:dropwizard-testing:$dropwizardVersion",
      'org.skyscreamer:jsonassert:1.3.0',
      'com.github.tomakehurst:wiremock:2.7.1',
      'org.awaitility:awaitility-groovy:3.0.0',
      'ru.yandex.qatools.embed:postgresql-embedded:2.6',
      'com.h2database:h2:1.4.196'

  //Pact
  testCompile 'au.com.dius:pact-jvm-provider-junit_2.11:3.3.10'
  testCompile 'au.com.dius:pact-jvm-consumer-junit_2.11:3.3.10'
}

pact {
  publish {
    pactDirectory = 'target/pacts'
    pactBrokerUrl = 'https://pact-broker.ci.uktrade.io/'
    version = '1.0.0'
  }
}

shadowJar {
  classifier = ''
  mergeServiceFiles()
}

jar {
  manifest {
    attributes 'Main-Class': mainClassName
  }
}

build {
  dependsOn shadowJar
}

run { args 'server', 'config.yaml' }

apply plugin: 'maven-publish'

def nexusUsername = project.hasProperty('nexusUsername') ? project.nexusUsername : ''
def nexusPassword = project.hasProperty('nexusPassword') ? project.nexusPassword : ''

publishing {
  repositories {
    maven {
      name "snapshots"
      url "https://nexus.ci.uktrade.io/repository/maven-snapshots/"
      credentials {
        username nexusUsername
        password nexusPassword
      }
    }
    maven {
      name "releases"
      url "https://nexus.ci.uktrade.io/repository/maven-releases/"
      credentials {
        username nexusUsername
        password nexusPassword
      }
    }
    maven {
      name "lite-builds"
      url "https://nexus.ci.uktrade.io/repository/lite-builds/"
      credentials {
        username nexusUsername
        password nexusPassword
      }
    }
  }
}

task apiJar(type: Jar, dependsOn: compileJava) {
  from sourceSets.main.output.classesDir
  include 'uk/gov/bis/lite/ogel/api/**/*'
}

task apiJarWithSources(type: Jar) {
  from sourceSets.main.allJava
  include 'uk/gov/bis/lite/ogel/api/**/*'
}

publishing {
  publications {
    api(MavenPublication) {
      artifactId 'lite-ogel-service-api'
      version '1.2'
      artifact apiJar
      artifact apiJarWithSources {
        classifier "sources"
      }
    }
    service(MavenPublication) {
      artifactId 'lite-ogel-service'
      from components.shadow
    }
  }
}
